; ATTACKING

(defconst flag-tsa 1)
(defconst flag-inside-safe 2)
(defconst flag-outside-safe 4)
(defconst flag-outside-max 8)

(defrule
	(true)
=>
	(set-strategic-number sn-percent-enemy-sighted-response 100)
	(set-strategic-number sn-enemy-sighted-response-distance 50)
	(set-strategic-number sn-zero-priority-distance 100)
	(set-strategic-number sn-enable-offensive-priority 1)
	(set-strategic-number sn-enable-patrol-attack 1)
	(set-strategic-number sn-local-targeting-mode 1)
	(set-strategic-number sn-minimum-attack-group-size 1)
	(set-strategic-number sn-maximum-attack-group-size 1)
	(set-strategic-number sn-disable-defend-groups flag-outside-max)
	(set-strategic-number sn-consecutive-idle-unit-limit 1)
	(set-strategic-number sn-wall-targeting-mode 1)
	;(set-strategic-number sn-gather-defense-units 1)
	(disable-self)
)

(defrule
	(true)
=>
	(set-strategic-number sn-number-civilian-militia 0)
	(set-strategic-number sn-allow-civilian-offense 0)
	(set-strategic-number sn-allow-civilian-defense 0)
	(disable-self)
)

; Switch Attack State

#if (sn-attack == YES) (gl-currently-attacking == NO)

	gl-currently-attacking = YES
	
	chat-local-to-self "Attack!"
	
#end-if

#if (sn-attack == NO) (gl-currently-attacking == YES)

	gl-currently-attacking = NO
	
	sn-maximum-town-size = sn-minimum-town-size
			
	up-full-reset-search
	up-find-local c: town-center c: 10
	up-find-local c: castle c: 10
	up-find-local c: house c: 50
	up-get-point position-self gl-point1
	up-set-target-point gl-point1
	up-clean-search search-local object-data-distance search-order-desc
	up-set-target-object search-local c: 0
	var gl-distance
	up-get-object-data object-data-distance gl-distance
	sn-maximum-town-size max= gl-distance
	
	sn-maximum-town-size += 5
			
	sn-total-number-explorers = 1
	sn-number-explore-groups = 1
	
	chat-local-to-self "Run away!"
	
#end-if

; Auto Attack

#if sn-auto-attack == ON

	var gl-pop
	var gl-pop-cap
	var gl-mod = 90
	
	var gl-ally-attacked = -1
	gl-ally-attacked = GetAllyAttacked()
	
	#if gl-ally-attacked > 0
	
		gl-mod = 50
		
	#end-if
	
	up-get-fact population 0 gl-pop
	up-get-fact population-cap 0 gl-pop-cap
	gl-pop-cap -= 10
	gl-pop-cap %*= gl-mod
		
	#if (sn-attack == NO) (sn-primary-unit != OFF) (sn-min-primary-unit >= 5) (timer-triggered ti-repeat-3m)
		
		var gl-should-attack = NO
		
		#if (gl-pop > gl-pop-cap) (military-population > 25)
		
			gl-should-attack = YES
		
		#end-if
		
		#if gl-should-attack == YES
		
			sn-attack = YES
			
		#end-if
		
	#end-if
		
	var gl-should-retreat = NO
		
	gl-pop-cap %*= 80
	#if or (gl-pop < gl-pop-cap) (military-population < 15)
		
		gl-should-retreat = YES
			
	#end-if
		
	#if sn-town-threat == HIGH
		
		gl-should-retreat = YES
			
	#end-if
		
	#if (up-object-type-count s: sn-siege-unit c:== 0) (unit-type-count battering-ram-line == 0) (unit-type-count packed-trebuchet-class == 0) (unit-type-count unpacked-trebuchet-class == 0)
		
		gl-should-retreat = YES
			
	#end-if
		
	up-full-reset-search
	up-find-local c: castle c: 10
	up-find-local c: town-center c: 10
	up-remove-objects search-local object-data-attack-timer c:< 10
	up-get-search-state gl-search-state
		
	; a castle or tc was attacked recently
	#if gl-local-total > 0
		
		up-clean-search search-local object-data-attack-timer search-order-desc
		up-set-target-object search-local c: 0
		var gl-hp
		var gl-min-hp
		up-get-object-data object-data-hitpoints gl-hp
		up-get-object-data object-data-maxhp gl-min-hp
		gl-min-hp /= 2
		
		; if it's lower than half health
		#if gl-hp <= gl-min-hp
		
			;chat-local-to-self "Aborting attack due to counter attack"
			gl-should-retreat = YES
			
		#end-if
			
	#end-if
	
	up-get-fact population 0 gl-pop
	up-get-fact population-cap 0 gl-pop-cap
	gl-pop += 5
	
	#if gl-pop >= gl-pop-cap
	
		gl-should-retreat = NO
		
	#end-if
		
	#if gl-should-retreat == YES
		
		sn-attack = NO
			
	#end-if
	
#end-if


; Unit Micro

;up-set-attack-stance -1 c: stance-defensive

#if gl-currently-attacking == YES

	#if (up-building-type-in-town c: town-center == 0) (up-building-type-in-town c: castle == 0) (up-building-type-in-town c: watch-tower == 0)
	
		sn-maximum-town-size += 2
		sn-maximum-town-size min= 1500
		
		#if (sn-maximum-town-size > 250) (military-population > 20)
		
			sn-total-number-explorers = 10
			sn-number-explore-groups = 10
			
		#end-if
	
	#else
	
		#if timer-triggered ti-repeat-7
	
			chat-local-to-self "Attacking..."
		
			var gl-units
		
			; get siege
			up-full-reset-search
			gl-units = GetSiegeUnits(search-remote)
		
			; if I have siege
			#if gl-units > 0
		
				; get siege unit furthest away
				up-get-point position-self gl-point1
				up-set-target-point gl-point1
				up-clean-search search-remote object-data-distance search-order-desc
				up-set-target-object search-remote c: 0
				up-get-point position-object gl-point1
				up-set-target-point gl-point1
				
				sn-focus-player-number = sn-target-player-number
				up-full-reset-search
				up-filter-distance c: -1 c: 15
				up-filter-include cmdid-military -1 -1 -1
				up-find-remote c: -1 c: 40
				up-get-search-state gl-search-state
				
				gl-units = GetAvailableMilitary(search-local)
				
				; if there are enemies near our siege
				#if gl-remote-total > 0
				
					; if I have sufficient units
					#if gl-units > 20
					
						; order the 10 closest to attack the lowest hp enemies
						up-clean-search search-local object-data-distance search-order-asc
						up-remove-objects search-local object-data-index c:>= 10
						
						var gl-max = 0
						up-set-target-object search-local c: 0
						var gl-range = 0
						up-get-object-data object-data-range gl-range
						#if gl-range <= 3
						
							gl-max = 2
							
						#end-if
						
						up-clean-search search-remote object-data-hitpoints search-order-asc
						up-remove-objects search-remote object-data-index g:> gl-max
						
						up-target-objects 0 action-default -1 -1
						
						up-get-search-state gl-search-state
						up-chat-data-to-self "Defending siege with %d" g: gl-local-total
						
					#end-if
						
				#else
				
					; guard our siege
					up-remove-objects search-local object-data-distance c:< 10
					up-target-objects 1 action-guard -1 -1
				
				#end-if
				
			#end-if
		
		#end-if
	
	#end-if

#else
	
	#if timer-triggered ti-repeat-7
		var gl-units
		
		up-full-reset-search
		gl-units = GetSiegeUnits(search-local)
		
		#if up-enemy-buildings-in-town == 0
		
			up-get-point position-self gl-point1
			up-set-target-point gl-point1
			up-remove-objects search-local object-data-distance s:< sn-minimum-town-size
		
			up-target-point gl-point1 action-move -1 -1
		
		#end-if
		
		var gl-mil-pop
		up-get-fact military-population 0 gl-mil-pop
		gl-mil-pop %*= 150
		#if up-enemy-units-in-town g:> gl-mil-pop
			
			up-gather-inside c: barracks c: 1
			up-gather-inside c: archery-range c: 1
			up-gather-inside c: stable c: 1
			up-gather-inside c: siege-workshop c: 1
			up-gather-inside c: castle c: 1
				
		#else
			
			up-gather-inside c: barracks c: 0
			up-gather-inside c: archery-range c: 0
			up-gather-inside c: stable c: 0
			up-gather-inside c: siege-workshop c: 0
			up-gather-inside c: castle c: 0
			
		#end-if
		
	#end-if
	
	#if (current-age < castle-age) (up-enemy-units-in-town < 10) (military-population < 10) (research-completed ri-loom)
		
		sn-number-civilian-militia = 15
		sn-allow-civilian-offense = 2
		sn-allow-civilian-defense = 3
		
	#else
	
		sn-number-civilian-militia = 0
		sn-allow-civilian-offense = 0
		sn-allow-civilian-defense = 0
		
	#end-if
	
#end-if


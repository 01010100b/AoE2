; STRATEGY BASIC

; TODO all civs

var gl-my-feudal-unit
var gl-my-primary-unit
var gl-my-secondary-unit
var gl-my-siege-unit
var gl-my-primary-trash-unit
var gl-my-secondary-trash-unit
var gl-my-infantry-counter
var gl-my-archer-counter
var gl-my-cavalry-counter
var gl-my-monk-counter
var gl-my-eagle-warrior-counter

(load "Binary\Strategies\BasicStrategyCivs")

sn-market-eco-balance = LOW

#if or (sn-gold-availability <= LOW) (current-age >= imperial-age)

	sn-market-eco-balance = HIGH
	
#end-if

; age up

#if (civilian-population >= 23)

	TryImportantResearch(feudal-age)
	
#end-if

#if (civilian-population >= 29)

	var gl-age-up = YES
	
	#if (sn-primary-unit != OFF) (up-resource-amount amount-military-population s:< sn-recent-target-army)
	
		;gl-age-up = NO
		
	#end-if
	
	#if can-research-with-escrow castle-age
	
		up-research gl-with-escrow c: castle-age
		
	#end-if
	
	#if gl-age-up == YES
	
		TryImportantResearch(castle-age)
	
	#end-if
	
#end-if

#if (civilian-population >= 45)

	var gl-age-up = YES
	
	#if (sn-primary-unit != OFF) (up-resource-amount amount-military-population s:< sn-recent-target-army)
	
		;gl-age-up = NO
		
	#end-if
	
	#if can-research-with-escrow imperial-age
	
		up-research gl-with-escrow c: imperial-age
		
	#end-if
	
	#if gl-age-up == YES
	
		TryImportantResearch(imperial-age)
	
	#end-if
	
#end-if

#if (sn-town-threat == HIGH) (sn-important-research == castle-age)

	;sn-important-research = OFF
	
#end-if

#if (sn-town-threat < MEDIUM) (up-research-status c: castle-age c:>= research-pending) (research-completed ri-double-bit-axe) (research-completed ri-horse-collar)

	TryImportantResearch(ri-gold-mining)
	TryImportantResearch(ri-stone-mining)
	
#end-if

; buildings

#if (current-age >= castle-age) (can-build castle) (or (players-military-population any-enemy > 10) (up-object-type-count c: town-center s:>= sn-min-town-center))

	BuildNormal(gl-without-escrow, castle)

#end-if

#if (up-research-status c: castle-age c:>= research-pending)

	sn-min-town-center max= 3
	
#end-if

#if (building-type-count castle > 0) (building-type-count university > 0)

	TryImportantResearch(ri-murder-holes)
	
#end-if

var gl-unit
up-get-object-type-data s: sn-primary-unit object-data-type gl-unit

#if (or (sn-primary-unit == OFF) (up-train-site-ready g: gl-unit)) (up-object-type-count-total c: town-center s:>= sn-min-town-center)

	#if (current-age >= feudal-age) (building-type-count-total market == 0) 

		TryImportantBuilding(market, BUILDING-NORMAL, -1, -1)
	
	#end-if

	#if (current-age >= feudal-age) (building-type-count-total blacksmith == 0) (building-type-count-total market > 0)

		TryImportantBuilding(blacksmith, BUILDING-NORMAL, -1, -1)
	
	#end-if

	#if (current-age >= castle-age) (building-type-count-total university == 0)

		TryImportantBuilding(university, BUILDING-NORMAL, -1, -1)
	
	#end-if
	
	#if (current-age >= castle-age) (building-type-count-total monastery == 0) (building-type-count-total university > 0)

		TryImportantBuilding(monastery, BUILDING-NORMAL, -1, -1)
	
	#end-if

#end-if

; units

#if up-research-status c: feudal-age c:>= research-pending

	sn-primary-unit = gl-my-feudal-unit
	sn-min-primary-unit = sn-recent-target-army

	sn-auto-attack = OFF
	
	var gl-biggest-threat = NO

	#if sn-monk-threat > gl-biggest-threat
	
		sn-primary-unit = gl-my-monk-counter
		gl-biggest-threat = sn-monk-threat
		
	#end-if
	
	#if sn-cavalry-threat > gl-biggest-threat
	
		sn-primary-unit = gl-my-cavalry-counter
		gl-biggest-threat = sn-cavalry-threat
		
	#end-if
	
	#if sn-eagle-warrior-threat > gl-biggest-threat
	
		sn-primary-unit = gl-my-eagle-warrior-counter
		gl-biggest-threat = sn-eagle-warrior-threat
		
	#end-if
	
	#if sn-archery-threat > gl-biggest-threat
	
		sn-primary-unit = gl-my-archer-counter
		gl-biggest-threat = sn-archery-threat
		
	#end-if
	
	#if sn-infantry-threat > gl-biggest-threat
	
		sn-primary-unit = gl-my-infantry-counter
		gl-biggest-threat = sn-infantry-threat
		
	#end-if
	
#end-if

#if up-research-status c: castle-age c:>= research-pending

	sn-primary-unit = gl-my-primary-unit
	
#end-if

#if current-age >= castle-age
	
	sn-auto-attack = ON
	
	sn-primary-unit = gl-my-primary-unit
	sn-secondary-unit = gl-my-secondary-unit
	
	#if up-object-type-count-total c: town-center s:>= sn-min-town-center
	
		sn-siege-unit = gl-my-siege-unit
		sn-min-siege-unit = 5
		
		#if (unit-available trebuchet) (unit-type-count-total trebuchet-set < 5)
	
			sn-siege-unit = trebuchet
		
		#end-if
	
	#end-if
	
	sn-min-primary-unit = 20
	sn-min-secondary-unit = 10
	
	#if sn-recent-target-army > 40
	
		sn-min-primary-unit = 40
		sn-min-secondary-unit = 20
		
	#end-if

	var gl-biggest-threat = NO
	
	#if sn-monk-threat > gl-biggest-threat
	
		sn-secondary-unit = gl-my-monk-counter
		gl-biggest-threat = sn-monk-threat
		
	#end-if
	
	#if sn-cavalry-threat > gl-biggest-threat
	
		sn-secondary-unit = gl-my-cavalry-counter
		gl-biggest-threat = sn-cavalry-threat
		
	#end-if
	
	#if sn-eagle-warrior-threat > gl-biggest-threat
	
		sn-secondary-unit = gl-my-eagle-warrior-counter
		gl-biggest-threat = sn-eagle-warrior-threat
		
	#end-if
	
	#if sn-archery-threat > gl-biggest-threat
	
		sn-secondary-unit = gl-my-archer-counter
		gl-biggest-threat = sn-archery-threat
		
	#end-if
	
	#if sn-infantry-threat > gl-biggest-threat
	
		sn-secondary-unit = gl-my-infantry-counter
		gl-biggest-threat = sn-infantry-threat
		
	#end-if	
	
	#if gl-biggest-threat == HIGH
	
		var gl-prime = sn-min-primary-unit
		sn-min-primary-unit = sn-min-secondary-unit
		sn-min-secondary-unit = gl-prime
		
	#end-if
	
	#if sn-gold-availability <= LOW
	
		sn-primary-unit = gl-my-primary-trash-unit
		sn-secondary-unit = gl-my-secondary-trash-unit
		
	#end-if
	
#end-if

; TODO switch this condition to when civilian pop cap is near rather than when imperial age
#if up-research-status c: imperial-age c:>= research-pending
	
	sn-auto-attack = ON
	
	var gl-pop-cap
	up-get-fact population-cap 0 gl-pop-cap
	
	var gl-mil = 95
	gl-mil -= sn-civilian-population-percentage
	gl-mil %*= gl-pop-cap
	
	#if sn-siege-unit != OFF
	
		gl-mil -= 5
		sn-min-siege-unit = 5
		
	#end-if
	
	#if sn-primary-unit != OFF
	
		sn-min-primary-unit = 60
		sn-min-primary-unit %*= gl-mil
		
	#end-if
	
	#if sn-secondary-unit != OFF
	
		sn-min-secondary-unit = 30
		sn-min-secondary-unit %*= gl-mil
		
	#end-if
	
	#if sn-tertiary-unit != OFF
	
		sn-min-tertiary-unit = 10
		sn-min-tertiary-unit %*= gl-mil
	
	#end-if
	
#end-if

#if (building-type-count monastery > 0) (unit-type-count-total monk == 0) (sn-important-unit == OFF)

	sn-important-unit = monk
	
#end-if
